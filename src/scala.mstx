import scala.templates
import scala.prelude
import scala.statements

main(prog) :- {s}
  // create a scope for the prelude
  // and populate it
  new s
  , prelude-ok(s)
  , compilation-units-ok(s, prog).

compilation-units-ok(s, cus) :- cus match
  { []      -> true
  | cu:cus' -> compilation-unit-ok(s, cu), compilation-units-ok(s, cus')
  }.

compilation-unit-ok(s, cu) :- {s_cu}
  new s_cu

  // the implicit import to the predef:
  , s_cu -[ `I ]-> s

  // we also mark this as the predefined module
  // to type literals correctly
  , s_cu -[ `PREDEF ]-> s

  // the body of the compilation unit
  , cu match
      { CompilationUnit(_, topstats) ->
          top-statements-ok(s_cu, topstats)
      }.
