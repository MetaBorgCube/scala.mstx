import scala.def
import scala.imports
import scala.type
import scala.expr

top-statements-ok(s, topstats) :- topstats match
  { st:stmts -> top-statement-ok(s, st), top-statements-ok(s, stmts)
  | []       -> true
  }.

stmts-ok(s_mod, s, stmts) :- stmts match
  { [] -> true
  | stmt:stmts -> {s'} stmt-ok(s_mod, s, stmt, s'), stmts-ok(s_mod, s', stmts)
  }.
  
block-statements-ok(s_block, s, bstmts, ty, s') :- bstmts match
  { [] -> s == s'
  | stmt:stmts -> {si, ty'} 
      block-statement-ok(s_block, s, stmt, ty', si), 
      block-statements-ok(s_block, si, stmts, ty, s')
  }.
  
block-statement-ok(s_block, s, bstmt, ty, s') :- bstmt match
  { DF2BS(def) -> 
        def-ok(s_block, s, def)
      , s' == s
      , predef-type(s, "Unit", ty)
  | EX2BS(e) -> 
        expr-ok(s, e, ty)
      , s' == s
  | IM2BS(ie) ->
        new s'
      , s' -[ `B ]-> s
      , import-ok(s', s_block, ie)
  }.

// top-level statements
top-statement-ok(s, stat) :- stat match

  // template definitions
  { DF2TS(ann, mod, def) -> template-def-ok(s, s, def)
  }.

stmt-ok(s_mod, s, stmt, s') :- stmt match

 // definitions
 { DF2TM(annot, modif, def) ->
     def-ok(s_mod, s, def)
     , s == s'

 // imports
 | IM2TM(imp) ->
     new s'
     , s' -[ `B ]-> s
     , import-ok(s', s, imp)

 }.
