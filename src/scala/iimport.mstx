import stdlib.paths
import scala.type.reference

iimports-stable-id(s_imp_nu, s_imp, stable_id, grp) :- {psteps}
    path-expr-with-path-steps(SID2P(stable_id), psteps)
  , iimports-path-steps-unqualified(s_imp_nu, s_imp, psteps, grp).

iimports-path-steps-unqualified(s_imp_nu, s_imp, psteps, grp) :- psteps match
  { [] -> false // Unreachable case
  | id:ids -> {s_step}
      resolve-unqualified-obj-scope(s_imp, id, s_step)
    , iimports-path-steps-qualified(s_imp_nu, s_step, ids, grp)
  }.

iimports-path-steps-qualified(s_imp, s, psteps, grp) :- psteps match
  { [] -> grp match
      { IId(id) -> {lbl, decl, s_iimport}
          resolve-qualified-decl(s, id, lbl, decl)
        , new s_iimport
        , s_imp -[ `IMPORT ]-> s_iimport
        , has-labeled-decl(s_iimport, lbl, decl)
      | IWild() ->
          s_imp -[ `WILDCARD ]-> s
      }
  | id:ids -> {s_step}
      resolve-qualified-obj-scope(s, id, s_step)
    , iimports-path-steps-qualified(s_imp, s_step, ids, grp)
  }.

has-labeled-decl(s, lbl, decl) :- lbl match
  { `DEF -> s -[ `DEF ]-> decl
  | `OBJ -> s -[ `OBJ ]-> decl }.
