import stdlib.sets
import stdlib.paths

import scala.type.reference
import scala.paths

no-duplicate-types(s, name, ty) :- {tydefs, matches}
    query s `TYPE as tydefs
  , filter tydefs ((name', ty') where name' == name) matches
  , singleton(matches).

type-decl(s_mod, s, name, ty) :- {s_ty, typ}
  new s_ty -> (name, typ)

  , s_mod -[ `TYPE ]-> s_ty
  , no-duplicate-types(s_mod, name, typ)

  , type-ok(s, ty, typ).

type-ok(s, t, ty) :- t match
  { IF2TY(t) -> annot-type-ok(s, t, ty) 
  | FunTy(l, r) -> {tyl, tyr} 
        param-type-ok(s, l, tyl)
      , type-ok(s, r, tyr)
      , ty == FUN(tyl, tyr)
  }.
  
param-type-ok(s, t, tys) :- t match
  { Single(t)    -> {ty} annot-type-ok(s, t, ty), tys == ty:[]
  | Multiple(ts) -> annot-types-ok(s, ts, tys)
  }.

path-type-ok(s, p, ty) :- p match
  { UnqualifiedId(id)   -> typeref-ok(s, id, ty)
  | QualifiedId(p', id) -> {s_obj, ps, op, s_d}
      resolve-path(s, p', s_obj)
      , resolve-local-decl(s_obj, id, ps)
      , only(ps, op)
      , tgt(op, s_d)
      , s_d -> (id, ty)
  }.

// check that tyname is a type def ty in Predef reachable from s
predef-type(s, tyname, ty) :- {tys, ps, p, n}
  query s `B*(`P`B*)*`PREDEF`TYPE as tys
  , filter tys ((name, _) where name == tyname) ps
  , only(ps, p)
  , datum(p, (n, ty)).
  
annot-type-ok(s, annot_t, ty) :- annot_t match
  { ST2AT(id, annot_opt) -> path-type-ok(s, id, ty) }.
  
annot-types-ok(s, ts, tys) :- ts match
  { []   -> tys == []
  | t:ts -> {ty, tys'} annot-type-ok(s, t, ty), annot-types-ok(s, ts, tys'), tys == ty:tys'
  }.
